##TERM_EXPANDER_DEMO
#SPARQL ENDPOINT (http://34.229.180.217:8080/fuseki/categories/query)
#SERVICE ENDPOINT (http://34.229.180.217:8080/fuseki/dataset.html?tab=upload&ds=/categories)

PREFIX cat: <http://www.esri-hub.com/categories/>
PREFIX hub: <http://www.esri-hub.com/vocab/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX ontology: <http://wordnet-rdf.princeton.edu/ontology#>

##SYNONYMS (skos:altLabel)
#agriculture - farming, husbandry, agribusiness...
#disaster - hazard, catastrophe, tragedy...
#housing - residences, slum clearance, affordable housing...
SELECT ?label ?synonym
WHERE {
?term skos:prefLabel ?label .
?term skos:altLabel ?synonym .
FILTER contains( lcase(?label), "agriculture")
}
ORDER BY ?synonym

##MULTILINGUAL (ontology:translation)
#disease - patología, ناخوشی, 病気...
#housing - casa, سَكَن, 住居施設...
#planning - conception, planificación, 予定...
SELECT ?term ?translation
WHERE {
?term skos:prefLabel ?label .
?same owl:sameAs ?term;
      ontology:translation ?translation .
FILTER contains( lcase(?label), "disease")
}
ORDER BY ?translation

##BROADER (skos:broader)
#infrastructure - economic development, human settlements, public works...
#health - public health, medicine, physiology
#sustainable - human ecology, social ecology
SELECT ?label ?prefLabel
WHERE {
?term skos:prefLabel ?label .
?same owl:sameAs ?term;
      skos:broader ?skos .
?skos skos:prefLabel ?prefLabel .
FILTER contains( lcase(?label), "infrastructure")
}
ORDER BY ?label

##NARROWER (skos:narrower)
#boundaries - maritime boundaries, ethnic boundaries, water boundaries...
#climate - bioclimatology, glaciers, seasons...
#demography - fertility, mortality, population density...
SELECT ?label ?prefLabel
WHERE {
?term skos:prefLabel ?label .
?same owl:sameAs ?term;
      skos:narrower ?skos .
?skos skos:prefLabel ?prefLabel .
FILTER contains( lcase(?label), "boundaries")
}
ORDER BY ?label

##RELATED (skos:related)
#crime - criminal justice, criminals, criminology...
#education - schools, teaching, training
#energy - energy harvesting, energy industries
SELECT ?label ?prefLabel
WHERE {
?term skos:prefLabel ?label .
?same owl:sameAs ?term;
      skos:related ?skos .
?skos skos:prefLabel ?prefLabel .
FILTER contains( lcase(?label), "crime")
}
ORDER BY ?label

##GENERIC tag search
#fire
#streets
#vehicle
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
SELECT ?s ?o
{ ?s skos:prefLabel ?o .
  FILTER regex(str(?o), "fire", "i")
}

##GENERIC tag search with related concepts
SELECT ?o ?a ?r
{ ?s skos:prefLabel ?o .
  OPTIONAL {
  ?s skos:altLabel ?a ;
     skos:related ?rel.
  ?rel skos:prefLabel ?r .
  }
  FILTER regex(str(?o), "road", "i")
}
ORDER BY DESC(?r)

SELECT ?label ?synonym ?translation ?broadLabel ?narrowLabel
WHERE {
?term skos:prefLabel ?label .
?term skos:altLabel ?synonym .
?term owl:sameAs ?same .
  OPTIONAL {
?same ontology:translation ?translation;
      skos:broader ?broader ;
      skos:narrower ?narrower .
?broader skos:prefLabel ?broadLabel .
?narrower skos:prefLabel ?narrowLabel .}
FILTER contains( lcase(?label), "transportation")
}
